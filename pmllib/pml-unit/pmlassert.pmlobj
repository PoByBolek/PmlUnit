-- Copyright (c) 2019 Florian Zimmermann.
-- Licensed under the MIT License: https://opensource.org/licenses/MIT
define object PmlAssert
endobject


define method .equal(!left is Any, !right is Any)

    !this.equal(!left, !right, 'Equality check failed.')

endmethod


define method .equal(!left is Any, !right is Any, !message is String)

    !this.isTrue(!left eq !right, !message)

endmethod


define method .notEqual(!left is Any, !right is Any)

    !this.notEqual(!left, !right, 'Inequality check failed.')

endmethod


define method .notEqual(!left is Any, !right is Any, !message is String)

    !this.isTrue(!left ne !right, !message)

endmethod


define method .lessThan(!left is Any, !right is Any)

    !this.lessThan(!left, !right, 'Less than check failed.')

endmethod


define method .lessThan(!left is Any, !right is Any, !message is String)

    !this.isTrue(!left lt !right, !message)

endmethod


define method .lessThanOrEqual(!left is Any, !right is Any)

    !this.lessThanOrEqual(!left, !right, 'Less than or equal check failed.')

endmethod


define method .lessThanOrEqual(!left is Any, !right is Any, !message is String)

    !this.isTrue(!left le !right, !message)

endmethod


define method .greaterThanOrEqual(!left is Any, !right is Any)

    !this.greaterThanOrEqual(!left, !right, 'Greater than or equal check failed.')

endmethod


define method .greaterThanOrEqual(!left is Any, !right is Any, !message is String)

    !this.isTrue(!left ge !right, !message)

endmethod


define method .greaterThan(!left is Any, !right is Any)

    !this.greaterThan(!left, !right, 'Greater than check failed.')

endmethod


define method .greaterThan(!left is Any, !right is Any, !message is String)

    !this.isTrue(!left gt !right, !message)

endmethod


define method .isSet(!value is Any)

    !this.isSet(!value, 'Value is not set.')

endmethod


define method .isSet(!value is Any, !message is String)

    !this.isTrue(!value.set(), !message)

endmethod


define method .isUnset(!value is Any)

    !this.isUnset(!value, 'Value is set.')

endmethod


define method .isUnset(!value is Any, !message is String)

    !this.isTrue(!value.unset(), !message)

endmethod


define method .isTrue(!value is Boolean)

    !this.isTrue(!value, 'Value is not true.')

endmethod


define method .isTrue(!value is Boolean, !message is String)

    if (!value.unset() or not !value) then
        !this.fail(!message)
    endif

endmethod


define method .isFalse(!value is Boolean)

    !this.isFalse(!value, 'Value is not false.')

endmethod


define method .isFalse(!value is Boolean, !message is String)

    if (!value.unset() or !value) then
        !this.fail(!message)
    endif

endmethod


define method .instanceOf(!object is Any, !objectType is String)

    !this.instanceOf(!object, !objectType, 'Expected object to be of type <$!<objectType>> but it is of type <' & !object.objectType() & '> instead.')

endmethod


define method .instanceOf(!object is Any, !objectType is String, !message is String)

    !this.equal(!object.objectType(), !objectType, !message)

endmethod


define method .fail()

    !this.fail('Test Failed.')

endmethod


define method .fail(!message is String)

    !!messageFile.error(196, 1, !message)

endmethod
